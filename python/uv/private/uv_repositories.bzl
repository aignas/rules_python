# Copyright 2024 The Bazel Authors. All rights reserved.
#
# Licensed under the Apache License, Version 2.0 (the "License");
# you may not use this file except in compliance with the License.
# You may obtain a copy of the License at
#
#    http://www.apache.org/licenses/LICENSE-2.0
#
# Unless required by applicable law or agreed to in writing, software
# distributed under the License is distributed on an "AS IS" BASIS,
# WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
# See the License for the specific language governing permissions and
# limitations under the License.

"""
EXPERIMENTAL: This is experimental and may be removed without notice

Create repositories for uv toolchain dependencies
"""

load(":toolchain_types.bzl", "UV_TOOLCHAIN_TYPE")
load(":uv_toolchains_repo.bzl", "uv_toolchains_repo")

UV_BUILD_TMPL = """\
# Generated by repositories.bzl
load("@rules_python//python/uv:uv_toolchain.bzl", "uv_toolchain")

uv_toolchain(
    name = "uv_toolchain",
    uv = "{binary}",
    version = "{version}",
)
"""

def _uv_repo_impl(repository_ctx):
    platform = repository_ctx.attr.platform

    is_windows = "windows" in platform
    _, _, filename = repository_ctx.attr.urls[0].rpartition("/")
    if filename.endswith(".tar.gz"):
        strip_prefix = filename[:-len(".tar.gz")]
    else:
        strip_prefix = ""

    result = repository_ctx.download_and_extract(
        url = repository_ctx.attr.urls,
        sha256 = repository_ctx.attr.sha256,
        stripPrefix = strip_prefix,
    )

    binary = "uv.exe" if is_windows else "uv"
    repository_ctx.file(
        "BUILD.bazel",
        UV_BUILD_TMPL.format(
            binary = binary,
            version = repository_ctx.attr.version,
        ),
    )

    return {
        "name": repository_ctx.attr.name,
        "platform": repository_ctx.attr.platform,
        "sha256": result.sha256,
        "urls": repository_ctx.attr.urls,
        "version": repository_ctx.attr.version,
    }

uv_repository = repository_rule(
    _uv_repo_impl,
    doc = "Fetch external tools needed for uv toolchain",
    attrs = {
        "platform": attr.string(mandatory = True),
        "sha256": attr.string(mandatory = False),
        "urls": attr.string_list(mandatory = True),
        "version": attr.string(mandatory = True),
    },
)

def uv_repositories(*, name, version, platforms, urls):
    """Convenience macro which does typical toolchain setup

    Skip this macro if you need more control over the toolchain setup.

    Args:
        name: The name of the toolchains repo,
        version: The uv toolchain version to download.
        platforms: The platforms to register uv for.
        urls: The urls with sha256 values to register uv for.
    """
    if not version:
        fail("version is required")

    toolchain_names = []
    toolchain_labels_by_toolchain = {}
    toolchain_compatible_with_by_toolchain = {}

    for platform_name, platform in platforms.items():
        uv_repository_name = "{}_{}".format(name, platform_name.lower().replace("-", "_"))
        uv_repository(
            name = uv_repository_name,
            version = version,
            platform = platform_name,
            urls = urls[platform_name].urls,
            sha256 = urls[platform_name].sha256,
        )

        toolchain_name = uv_repository_name + "_toolchain"
        toolchain_names.append(toolchain_name)
        toolchain_labels_by_toolchain[toolchain_name] = "@{}//:uv_toolchain".format(uv_repository_name)
        toolchain_compatible_with_by_toolchain[toolchain_name] = [
            str(label)
            for label in platform.compatible_with
        ]

    uv_toolchains_repo(
        name = name,
        toolchain_type = str(UV_TOOLCHAIN_TYPE),
        toolchain_names = toolchain_names,
        toolchain_labels = toolchain_labels_by_toolchain,
        toolchain_compatible_with = toolchain_compatible_with_by_toolchain,
    )
